//// Создание актов 

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура СоздатьАкты(Команда)
	
	ИДЗадания = "";
	Индикатор = 0;
	СтрокаСостояния = "";   
	
	ПараметрыЗапуска = Новый Структура;
	ПараметрыЗапуска.Вставить("Период", Объект.Период);
		
	СтруктураФоновогоЗадания = ВыполнитьФоновоеЗаданиеНаСервере(ПараметрыЗапуска, УникальныйИдентификатор);
	ИДЗадания 	= СтруктураФоновогоЗадания.ИдентификаторЗадания;
	
	ПараметрыОжидания  = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
	ПараметрыОжидания.Интервал 	= 2;
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(СтруктураФоновогоЗадания, Новый ОписаниеОповещения("ОповещениеОЗавершении", ЭтотОбъект), ПараметрыОжидания);
	
	  
	
КонецПроцедуры       

&НаКлиенте
Процедура Заполнить(Команда)
	ЗаполнитьНаСервере();
КонецПроцедуры
#КонецОбласти

 
#Область СлужебныеПроцедурыИФункции  
 
&НаСервере
Функция ВыполнитьФоновоеЗаданиеНаСервере(ПараметрыЗапуска, УникальныйИдентификатор)
	
	НаименованиеЗадания = "Запуск длительной операции";
	ВыполняемыйМетод = "ВКМ_ЗаполнениеДокументов.ЗаполнитьДокументы";
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НаименованиеЗадания;
	ПараметрыВыполнения.ЗапуститьВФоне 	= Истина;
	ПараметрыВыполнения.Вставить("ИдентификаторФормы", УникальныйИдентификатор); 
	
	СтруктураФоновогоЗадания = ДлительныеОперации.ВыполнитьВФоне(ВыполняемыйМетод, ПараметрыЗапуска, ПараметрыВыполнения);
	
		
	Возврат СтруктураФоновогоЗадания;
	
КонецФункции

&НаКлиенте
Процедура ОповещениеОЗавершении(Результат, ДополнительныеПараметры) Экспорт
    		
	Если Результат.Статус = "Выполнено" Тогда 
		ТекстСообщения = "Акты успешно созданы " + ПолучитьИзВременногоХранилища(Результат.АдресРезультата); 
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		ЗаполнитьНаСервере();    
	ИначеЕсли Результат.Статус = Неопределено Тогда
		ПоказатьПредупреждение(, "Операция не выполнена (нет результата)");
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		ПоказатьПредупреждение(, "Ошибка при создании актов");
	КонецЕсли;
	
КонецПроцедуры   

&НаСервере
Процедура ЗаполнитьНаСервере() 
	
	Объект.Договоры.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|   ДоговорыКонтрагентов.Ссылка,
	|   ДоговорыКонтрагентов.Наименование,
	|   ДоговорыКонтрагентов.Организация,
	|   ДоговорыКонтрагентов.Владелец
	|ИЗ
	|   Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|   НЕ ДоговорыКонтрагентов.ПометкаУдаления
	|   И ДоговорыКонтрагентов.ВидДоговора = &ВидДоговора
	|   И ДоговорыКонтрагентов.ВКМ_ДатаНачалаАбонентскогоОбслуживания <= &ДатаНачала";
	
	Запрос.УстановитьПараметр("ВидДоговора", Перечисления.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание);  
	Запрос.УстановитьПараметр("ДатаНачала", НачалоМесяца(Объект.Период));
	
	ТаблицаДоговоров = Запрос.Выполнить().Выгрузить();
	
	МассивДоговоров = Новый Массив;
	Для Каждого СтрокаДоговора Из ТаблицаДоговоров Цикл
		МассивДоговоров.Добавить(СтрокаДоговора.Наименование);
	КонецЦикла;
	
	ЗапросРеализаций = Новый Запрос;
	ЗапросРеализаций.Текст = 
	"ВЫБРАТЬ
	|   РеализацияТоваровУслуг.Договор.Наименование КАК НаименованиеДоговора,
	|   РеализацияТоваровУслуг.Ссылка КАК Ссылка,
	|   РеализацияТоваровУслуг.Комментарий КАК Комментарий
	|ИЗ
	|   Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|   РеализацияТоваровУслуг.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|   И РеализацияТоваровУслуг.Договор.ВидДоговора = &ВидДоговора
	|   И РеализацияТоваровУслуг.Договор.Наименование В (&МассивДоговоров)
	|   И НЕ РеализацияТоваровУслуг.ПометкаУдаления";
	
	ЗапросРеализаций.УстановитьПараметр("ВидДоговора", Перечисления.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание);
	ЗапросРеализаций.УстановитьПараметр("ДатаНачала", НачалоМесяца(Объект.Период));
	ЗапросРеализаций.УстановитьПараметр("ДатаОкончания", КонецМесяца(Объект.Период));
	ЗапросРеализаций.УстановитьПараметр("МассивДоговоров", МассивДоговоров);
	
	РеализацииДоговоров = Новый Соответствие;
	ТаблицаРеализаций = ЗапросРеализаций.Выполнить().Выгрузить();
	
	Для Каждого СтрокаРеализации Из ТаблицаРеализаций Цикл
		РеализацииДоговоров[СтрокаРеализации.НаименованиеДоговора] = Новый Структура("Ссылка,Комментарий", 
		СтрокаРеализации.Ссылка, 
		СтрокаРеализации.Комментарий);
	КонецЦикла;
	
	Для Каждого СтрокаДоговора Из ТаблицаДоговоров Цикл
		НоваяСтрока = Объект.Договоры.Добавить();
		НоваяСтрока.Договор = СтрокаДоговора.Ссылка;
		
		Если РеализацииДоговоров.Получить(СтрокаДоговора.Наименование) <> Неопределено Тогда 
			ДанныеРеализации = РеализацииДоговоров[СтрокаДоговора.Наименование];
			НоваяСтрока.Реализация = ДанныеРеализации.Ссылка;
			НоваяСтрока.Комментарий = ДанныеРеализации.Комментарий;
		Иначе
			НоваяСтрока.Реализация = "";
			НоваяСтрока.Комментарий = "Реализация отсутствует";
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры
#КонецОбласти

